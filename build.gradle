plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.4'
    id 'io.spring.dependency-management' version '1.1.3'
    id 'jacoco'
    id 'checkstyle'
}
checkstyle {
    configFile = file("${project.rootDir}/config/checkstyle/google_checks.xml")
    toolVersion = '10.12.4'
    reportsDir = file("${project.buildDir}/checkstyle")
    showViolations = false
    maxWarnings = 150
    maxErrors = 10
}
tasks.withType(Checkstyle) {
    reports {
        xml.required = false
        html.required = true
    }
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.rest-assured:rest-assured:5.3.2'
}

sourceSets {
    integrationTest {
        java {
            java.srcDir file('src/integrationTest/java')
            resources.srcDir file('src/integrationTest/resources')
            compileClasspath += sourceSets.main.output + sourceSets.test.output
            runtimeClasspath += sourceSets.main.output + sourceSets.test.output
        }
        resources.srcDir file('src/integrationTest/resources')
    }
    systemTest {
        java.srcDir file('src/systemTest/java')
        resources.srcDir file('src/systemTest/resources')
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
    systemTestImplementation.extendsFrom testImplementation
    systemTestRuntimeOnly.extendsFrom testRuntimeOnly
}

test {
    useJUnitPlatform {
        failFast = true
    }
    testLogging {
        events "passed", "skipped", "failed"
    }
    finalizedBy jacocoTestReport
    finalizedBy checkstyleMain
    finalizedBy checkstyleTest
}

task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    useJUnitPlatform {
        failFast = true
    }
    testLogging {
        events "passed", "skipped", "failed"
    }
    finalizedBy jacocoTestReport
    finalizedBy checkstyleIntegrationTest
}

task systemTest(type: Test) {
    testClassesDirs = sourceSets.systemTest.output.classesDirs
    classpath = sourceSets.systemTest.runtimeClasspath
    useJUnitPlatform {
        failFast = true
    }
    testLogging {
        events "passed", "skipped", "failed"
    }
    finalizedBy jacocoTestReport
    finalizedBy checkstyleSystemTest
}

jacocoTestReport {
    reports {
        xml.required = true
        html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
        html.required = true
    }
}


